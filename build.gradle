//use: gradlew clean build fatJar signArchives install bintrayUpload
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

def versionObj = new Version(major: 2, minor: 2, revision: 0)
group = "net.dv8tion"
archivesBaseName = "JDA"
version = "$versionObj"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.eclipse.dependsOn(cleanEclipse)

def filteredSourceDir = file("$buildDir/filtered")

// This is to fix eclipse's stupidity when first setting up the project.
// It thinks that "filtered" should be a development source folder and cries when it doesn't exist (gradlew clean)
eclipse {
    classpath {
        file {
            withXml {
                def sb = it.asString()
                sb.replace(0, sb.length(), sb.toString().replace("\n\t<classpathentry kind=\"src\" path=\"build/filtered\"/>", ""));
            }
        }
    }
}


sourceSets {
    // This source set will contain all sources that we filter
    filtered {
        java {
            srcDirs = [
                filteredSourceDir,
                "src/test/java",
                "src/examples/java"
            ]
        }
    }
}

javadoc {
    failOnError = false

    //### excludes ###
    //jda internals
    exclude("net/dv8tion/jda/entities/impl")
    exclude("net/dv8tion/jda/managers/impl")
    exclude("net/dv8tion/jda/handle")
    exclude("net/dv8tion/jda/requests")

    exclude("net/dv8tion/jda/audio/AudioConnection.java")
    exclude("net/dv8tion/jda/audio/AudioPacket.java")
    exclude("net/dv8tion/jda/audio/AudioWebSocket.java")
    //org.tritonus
    exclude("org/tritonus")
    //opuswrapper
    exclude("tomp2p/opuswrapper")
    //voice crypto
    exclude("com/iwebpp/crypto")
}

// copy the main sources and filter any '@buildVersion@' occurences. 
task processVersion (type: Copy) {
    from sourceSets.main.java
    into filteredSourceDir
    filter(ReplaceTokens, tokens: [
        versionMajor: versionObj.major.toString(),
        versionMinor: versionObj.minor.toString(),
        versionRevision: versionObj.revision.toString(),
        versionBuild: versionObj.build.toString()
    ])
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
    }
    include "net/dv8tion/jda/**"
    include "com/iwebpp/crypto/**"
    include "org/tritonus/**"
    include "tomp2p/opuswrapper/**"
    include "natives/**"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.filtered.java
    include "net/dv8tion/jda/**"
    include "com/iwebpp/crypto/**"
    include "org/tritonus/**"
    include "tomp2p/opuswrapper/**"
}

// create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version
    }
    
    baseName = project.name + '-withDependencies'
    from { 
        configurations.compile.collect {
            dependency ->
            if (dependency.directory) { // If it is a folder, just include the folder in its entirety
                return dependency
            } else {    // If it isn't a folder, put it in a zipTree. if it is a zip or jar the contents will be extracted.
                return zipTree(dependency)
            }
        }
    }
    with jar
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
repositories {
    jcenter()
}

dependencies {
    //General Utility
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.json:json:20150729'

    //Native Library Support
    compile 'net.java.dev.jna:jna:4.2.1'

    //Web Connection Support
    compile 'com.neovisionaries:nv-websocket-client:1.16'
    compile 'com.mashape.unirest:unirest-java:1.4.7'

    //Audio Support
    compile 'com.googlecode.soundlibs:tritonus-share:0.3.7-2'   //Shared audio code
    compile 'org.tritonus:tritonus-dsp:0.3.6'                   //Volume control
    compile 'com.googlecode.soundlibs:mp3spi:1.9.5-1'           //MP3 support
    compile 'net.sourceforge.jaadec:jaad:0.8.5'                 //AAC supports
    compile 'jflac:jflac:1.3'                                   //FLAC support
}

class Version {
    int major, minor, revision

    String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    pkg {
        repo = 'maven'
        name = 'JDA'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/DV8FromTheWorld/JDA.git'
        filesSpec {
            from ('build/libs') {
                exclude "**withDependencies**"
            }
            from 'build/poms'
            into "${project.group.replace(".", "/")}/${archivesBaseName}/${project.version}"

            rename { String fileName ->
                fileName.replace("pom-default.xml", "${archivesBaseName}-${project.version}.pom")
            }
        }
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    onlyIf {
        System.getenv("BUILD_NUMBER")
    }
}

String getProjectProperty(String propertyName)
{
    String property = ""
    if (hasProperty(propertyName))
    {
        property = this.properties[propertyName]
    }
    return property
}

// tell the compileJava task to compile the filtered source
compileJava.source = sourceSets.filtered.java
compileJava.dependsOn processVersion

//use: gradlew clean build fatJar signArchives install bintrayUpload

// If we don't have the username and password for uploading, we probably also
// can't sign the archives, so skip these tasks.
signArchives.onlyIf {(!getProjectProperty("signing.keyId").empty
        && !getProjectProperty("signing.password").empty
        && !getProjectProperty("signing.secretKeyRingFile").empty) }
bintrayUpload.onlyIf { (!getProjectProperty("bintrayUsername").empty
        && !getProjectProperty("bintrayApiKey").empty) }
